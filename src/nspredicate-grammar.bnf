Predicate ::= ComparisonPredicate
   | CompoundPredicate
   | "(" Predicate ")"

CompoundPredicate ::= Predicate "AND" Predicate
   | Predicate "OR" Predicate
   | "NOT" Predicate

ComparisonPredicate ::= expression operation expression
   | aggregate_qualifier ComparisonPredicate

operation ::= "=" | "!=" | "<" | ">" | "<=" | ">="
   | BETWEEN
   | aggregate_operations [ "[" string_options "]" ]

aggregate_operations ::= CONTAINS | IN | string_operations

string_operations ::= BEGINSWITH | ENDSWITH | LIKE | MATCHES

string_options ::= c | d | cd

aggregate_qualifier ::= ANY | ALL | NONE | SOME

expression ::= "(" expression ")"
   | binary_expression
   | function_expression
   | assignment_expression
   | index_expression
   | keypath_expression
   | value_expression

value_expression ::= literal_value | literal_aggregate

literal_value ::= string_value
   | numeric_value
   | predicate_argument
   | predicate_variable
   | NULL
   | TRUE
   | FALSE
   | SELF

string_value ::= "text" | 'text'

predicate_argument ::= "%" format_argument

format_argument ::= "@" | "%" | "K"
    printf style conversion character

predicate_variable ::= "$" identifier

keypath_expression ::= identifier | "@" identifier
    | expression "." expression

literal_aggregate ::= "{" [ expression [ "," expression ... ] ] "}"

index_expression ::= array_expression "[" integer_expression "]"
    | dictionary_expression   "[" expression "]"
    | aggregate_expression "[" FIRST "]"
    | aggregate_expression "[" LAST "]"
    | aggregate_expression "[" SIZE "]"

aggregate_expression ::= array_expression | dictionary_expression

assignment_expression ::= predicate_variable ":=" expression

binary_expression ::= expression binary_operator expression
    | "-" expression

binary_operator ::= "+" | "-" | "*" | "/" | "**"

function_expression ::= function_name "(" [ expression [ "," expression ... ] ] ")"

function_name ::= "sum" | "count" | "min" | "max"
    | "average" | "median" | "mode" | "stddev"
    | "sqrt" | "log" | "ln" | "exp"
    | "floor" | "ceiling" | "abs" | "trunc"
    | "random" | "randomn" | "now"
